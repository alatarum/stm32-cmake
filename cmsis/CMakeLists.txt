PROJECT(stm32cmsis)

CMAKE_MINIMUM_REQUIRED(VERSION 2.6)
ENABLE_LANGUAGE(ASM)

IF(NOT STM32_SPL_DIR)
	SET(STM32_SPL_DIR "/opt/STM32F10x_StdPeriph_Lib_V3.5.0")
	MESSAGE(STATUS "No STM32_SPL_DIR specified, using default: " ${STM32_SPL_DIR})
ENDIF()

IF(NOT STM32_FAMILY)
	SET(STM32_FAMILY "F10x")
	MESSAGE(STATUS "No STM32_FAMILY specified, using default: " ${STM32_FAMILY})
ENDIF()
STRING(TOLOWER "${STM32_FAMILY}" STM32_FAMILY_L)
STRING(TOUPPER "${STM32_FAMILY}" STM32_FAMILY_U)
MESSAGE(STATUS "Building for STM32${STM32_FAMILY_U} family")

IF(STM32_FAMILY_U STREQUAL "F10X")
	INCLUDE_DIRECTORIES(
		${STM32_SPL_DIR}/Libraries/CMSIS/CM3/DeviceSupport/ST/STM32F10x/
		${STM32_SPL_DIR}/Libraries/CMSIS/CM3/CoreSupport/
	)
	SET(STM32_CMSIS_HEADERS
		${STM32_SPL_DIR}/Libraries/CMSIS/CM3/DeviceSupport/ST/STM32F10x/system_stm32f10x.h
		${STM32_SPL_DIR}/Libraries/CMSIS/CM3/DeviceSupport/ST/STM32F10x/stm32f10x.h
		${STM32_SPL_DIR}/Libraries/CMSIS/CM3/CoreSupport/core_cm3.h
	)
	SET(CMSIS_SOURCES
		${STM32_SPL_DIR}/Libraries/CMSIS/CM3/DeviceSupport/ST/STM32F10x/system_stm32f10x.c
		${STM32_SPL_DIR}/Libraries/CMSIS/CM3/CoreSupport/core_cm3.c
	)
	SET(STM32_SPL_STARTUP_PATH ${STM32_SPL_DIR}/Libraries/CMSIS/CM3/DeviceSupport/ST/STM32F10x/startup/gcc_ride7/)
	SET(DENSITY_LIST_U HD HD_VL MD MD_VL LD LD_VL XD CL)
ELSEIF(STM32_FAMILY_U STREQUAL "L1XX")
	INCLUDE_DIRECTORIES(
		${STM32_SPL_DIR}/Libraries/CMSIS/Device/ST/STM32L1xx/Include/
		${STM32_SPL_DIR}/Libraries/CMSIS/Include/
	)
	SET(STM32_CMSIS_HEADERS
		${STM32_SPL_DIR}/Libraries/CMSIS/Device/ST/STM32L1xx/Include/system_stm32l1xx.h
		${STM32_SPL_DIR}/Libraries/CMSIS/Device/ST/STM32L1xx/Include/stm32l1xx.h
		${STM32_SPL_DIR}/Libraries/CMSIS/Include/core_cm3.h
		${STM32_SPL_DIR}/Libraries/CMSIS/Include/core_cmFunc.h
		${STM32_SPL_DIR}/Libraries/CMSIS/Include/core_cmInstr.h
	)
	SET(CMSIS_SOURCES
		${STM32_SPL_DIR}/Libraries/CMSIS/Device/ST/STM32L1xx/Source/Templates/system_stm32l1xx.c
	)
	SET(STM32_SPL_STARTUP_PATH ${STM32_SPL_DIR}/Libraries/CMSIS/Device/ST/STM32L1xx/Source/Templates/gcc_ride7/)
	SET(DENSITY_LIST_U HD MD MDP)
ELSE()
	MESSAGE(FATAL_ERROR "Unknown stm32 family.")
ENDIF()

INCLUDE_DIRECTORIES(
	${CMAKE_CURRENT_SOURCE_DIR}
)

FOREACH(DENSITY_U ${DENSITY_LIST_U})
	STRING(TOLOWER "${DENSITY_U}" DENSITY_L)

	SET(STM32_STARTUP_${DENSITY_U} ${STM32_SPL_STARTUP_PATH}/startup_stm32${STM32_FAMILY_L}_${DENSITY_L}.s)
	SET(STM32_STARTUPS ${STM32_STARTUPS} ${STM32_STARTUP_${DENSITY_U}})

	ADD_LIBRARY(stm32${STM32_FAMILY_L}_cmsis_${DENSITY_L} ${CMSIS_SOURCES})
	SET_TARGET_PROPERTIES(stm32${STM32_FAMILY_L}_cmsis_${DENSITY_L} PROPERTIES COMPILE_FLAGS "-DSTM32${STM32_FAMILY_U}_${DENSITY_U}")
	SET(CMSIS_TARGETS ${CMSIS_TARGETS} stm32${STM32_FAMILY_L}_cmsis_${DENSITY_L})
ENDFOREACH(DENSITY_U)


INSTALL(TARGETS ${CMSIS_TARGETS}
    RUNTIME DESTINATION bin
    LIBRARY DESTINATION lib
    ARCHIVE DESTINATION lib
)

INSTALL(FILES
	${STM32_CMSIS_HEADERS}
	DESTINATION
	include/stm32${STM32_FAMILY_L}
)

INSTALL(FILES
	${STM32_STARTUPS}
	DESTINATION
	share/cmsis/
)

INSTALL(FILES
	${CMAKE_CURRENT_SOURCE_DIR}/stm32_flash.ld.in
	RENAME
	stm32${STM32_FAMILY_L}_flash.ld.in
	DESTINATION
	share/cmsis/
)

