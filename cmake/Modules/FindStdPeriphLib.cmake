IF(NOT STM32_CHIP_TYPE)
	MESSAGE(FATAL_ERROR "Select your stm32 chip using STM32_CHIP_TYPE variable. (HD, HD_VL, MD, MDP, MD_VL, LD, LD_VL, XD, CL)")
ENDIF()

IF(NOT STM32_FAMILY)
	SET(STM32_FAMILY "F10x")
	MESSAGE(STATUS "No STM32_FAMILY specified, using default: " ${STM32_FAMILY})
ENDIF()
STRING(TOLOWER "${STM32_FAMILY}" STM32_FAMILY_L)
STRING(TOUPPER "${STM32_FAMILY}" STM32_FAMILY_U)
STRING(TOUPPER "${STM32_CHIP_TYPE}" STM32_CHIP_TYPE)

IF(STM32_FAMILY_U STREQUAL "F10X")
	SET(DENSITY_LIST_U HD HD_VL MD MD_VL LD LD_VL XD CL)
ELSEIF(STM32_FAMILY_U STREQUAL "L1XX")
	SET(DENSITY_LIST_U HD MD MDP)
ELSE()
	MESSAGE(FATAL_ERROR "Unknown stm32 family.")
ENDIF()

FOREACH(DENSITY_U ${DENSITY_LIST_U})
	IF(DENSITY_U STREQUAL STM32_CHIP_TYPE)
		STRING(TOLOWER "${STM32_CHIP_TYPE}" STM32_CHIP_TYPE_LOWER)
	ENDIF()
ENDFOREACH(DENSITY_U)

IF(NOT STM32_CHIP_TYPE_LOWER)
	MESSAGE(FATAL_ERROR "Specified wrong chip type: there is no ${STM32_CHIP_TYPE} type in the STM32${STM32_FAMILY} family")
ENDIF()


SET(STM_SPL_LIB_NAME stm32${STM32_FAMILY_L}_stdperiph_${STM32_CHIP_TYPE_LOWER})

FIND_PATH(STM_SPL_INCLUDE_DIR
	stm32${STM32_FAMILY_L}.h
	PATH_SUFFIXES include stm32${STM32_FAMILY_L}
)

FIND_LIBRARY(STM_SPL_LIBRARIES
	NAMES ${STM_SPL_LIB_NAME}
	PATH_SUFFIXES lib
)

SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -DUSE_STDPERIPH_DRIVER")
SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -DUSE_STDPERIPH_DRIVER")

INCLUDE(FindPackageHandleStandardArgs)
FIND_PACKAGE_HANDLE_STANDARD_ARGS(StdPeriphLib DEFAULT_MSG STM_SPL_LIBRARIES STM_SPL_INCLUDE_DIR)
